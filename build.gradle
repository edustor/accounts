import org.ajoberstar.grgit.Grgit

import java.time.Instant

buildscript {
    ext.kotlin_version = '1.0.4'
    ext.spring_boot_version = '1.4.1.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

plugins {
    id "com.palantir.configuration-resolver" version "0.1.0"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'

group 'ru.edustor'
version '0.6.0'

jar {
    destinationDir = new File(buildDir, "/dist")
    archiveName = "edustor-accounts.jar"
}

repositories {
    mavenCentral()
}

ext {
    def grgit = Grgit.open()
    shortCommitId = grgit.head().getAbbreviatedId()
    commitId = grgit.head().id
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build.properties").withWriter { w ->
            def p = new Properties()
            p['edustor.build.version'] = "${project.version}-$shortCommitId".toString()
            p['edustor.build.date'] = Instant.now().toString()
            p['edustor.build.commitId'] = commitId
            p.store(w, null)
        }
    }
}

processResources.finalizedBy createProperties

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-freemarker"

    compile 'org.postgresql:postgresql:9.4.1211'
    compile 'io.jsonwebtoken:jjwt:0.7.0'
    compile 'com.google.api-client:google-api-client:1.21.0'

    compile 'org.hibernate:hibernate-core:5.2.3.Final'
}
